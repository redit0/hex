aptHelp = function()
    text = g.const.ni + g.l3("Usage:","u") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-i/install] [library.so] {path}") + g.l2(" Installs whichever library or program is specified in the parameters.  If a path is provided, the library is installed at that location, otherwise, it is installed in whatever location HEX is run from.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-l/list]") + g.l2(" Lists all available libraries and programs from all sources.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-s/sources]") + g.l2(" Lists all apt repositories that have been added to sources.txt.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-a/add] {ip}") + g.l2(" Adds the specified IP address as a repository in sources.txt.  If no IP address is specified, adds a random hackshop.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-r/remove] [ip]") + g.l2(" Removes the specified IP address from the list of repositories in sources.txt.") + g.const.ni
    text = text + g.g2(self.name) + g.b3(" [-h/help]") + g.l2(" Displays this help text.") + g.const.nl

    print(text)
end function

apt = function(args)
    context = g.stack[g.context.index]

    if not context.apt then
        print(g.error("Apt-get is not available in this context. (If this is a shell, try running 'jump'.)"))
        return
    end if

///////////////////////////////////////////////////////////////////////////
//                               Functions                               //
///////////////////////////////////////////////////////////////////////////

    sources = function
        repos = g.func.getRepos

        output = g.const.nl
        for repo in repos
            output = output + g.const.indent + g.l1(repo) + g.const.nl
        end for

        print output
    end function

    add = function(address)
        return context.apt.add_repo(address)
    end function

    remove = function(address)
        return context.apt.del_repo(address)
    end function

    getPackageList = function
        repos = g.func.getRepos

        packages = {}

        for repo in repos
            packageList = context.apt.show(repo)

            if packageList.is_match(char(10) + char(10)) then
                tempList = packageList.split(char(10) + char(10))

                for package in tempList
                    if package == "" then continue
                    parts = package.split(char(10))

                    name = parts[0]
                    desc = parts[1]

                    packages[name] = desc
                end for
            else
                print g.error(packageList)
            end if
        end for

        return packages
    end function

    if args == null or args == [] then
        self.help
        return
    else
        cmd = args[0]

        opt = null
        if args.len >= 2 then opt = args[1]

        if cmd == "help" or cmd == "-h" then
            self.help
            return
        else if cmd == "sources" or cmd == "-s" then
            sources()
            return
        else if cmd == "add" or cmd == "-a" then
            if not opt then
                hackshop = g.func.getHackshop

                result = add(hackshop)

                if typeof(result) == "string" and result.len > 0 then
                    print(g.error(result))
                else
                    result = context.apt.update()
                    if typeof(result) == "string" and result != "" then
                        print(g.error(result))
                    else
                        print(g.const.indent + g.g1("Successfully added repository: " + opt))
                    end if
                end if
                return
            else if not is_valid_ip(opt) then
                print(g.error("Invalid parameter: " + opt))
                return
            else
                result = add(opt)

                if typeof(result) == "string" and result.len > 0 then
                    print(g.error(result))
                else
                    result = context.apt.update()
                    if typeof(result) == "string" and result != "" then
                        print(g.error(result))
                    else
                        print(g.const.indent + g.g1("Successfully added repository: " + opt))
                    end if
                end if
                return
            end if
        else if cmd == "remove" or cmd == "-r" then
            if not opt then
                print(g.error("Invalid number of arguments. (Usage: 'apt-get -r [ip address]')"))
                return
            else if not is_valid_ip(opt) then
                print(g.error("Invalid parameter: " + opt))
                return
            else
                result = remove(opt)

                if typeof(result) == "string" and result.len > 0 then
                    print(g.error(result))
                else
                    print(g.const.indent + g.g1("Successfully removed repository: " + opt))
                end if
                return
            end if
        else if cmd == "list" or cmd == "-l" then
            packages = getPackageList

            if packages == null then return

            output = g.const.nl

            indexes = packages.indexes

            for index in indexes.sort
                output = output + g.const.indent + g.g2(index + ": ") + g.l2(packages[index]) + g.const.nl + g.const.nl
            end for

            print output

            return
        else if cmd == "install" or cmd == "-i" then
            if not opt then
                print(g.error("Invalid number of arguments. (Usage: 'apt-get -i [library.so] {path}')"))
                return
            else
                if args.len == 3 then
                    path = args[2]
                else
                    path = context.path
                end if

                result = context.apt.install(opt, path)

                if typeof(result) == "string" and result.len > 0 then
                    print(g.error(result))
                else
                    print(g.const.indent + g.g1("Successfully installed: " + opt))
                end if
                return
            end if
        end if
    end if
end function

g.util["apt-get"] = {
    "name": "apt-get",
    "shortDescription": "Manages apt repositories and installs libraries.",
    "exec": @apt,
    "help": @aptHelp,
    "data": {}
}