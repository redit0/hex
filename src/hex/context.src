map.createContext = function
    item = new g.Target
    item.path = "/"
    item.type = typeof(self)

    if item.type == "shell" then
        item.shell = self
        item.computer = self.host_computer
        item.file = self.host_computer.File(item.path)
        item.public = self.host_computer.public_ip
        item.local = self.host_computer.local_ip
    else if item.type == "computer" then
        item.computer = self
        item.file = self.File(item.path)
        item.public = self.public_ip
        item.local = self.local_ip
    else if item.type == "file" then
        item.file = self
    else
        print(g.error("Invalid object type: " + item.type))
        print(self)
        return null
    end if

    item.user = g.func.getUser(item.file)

    return item
end function