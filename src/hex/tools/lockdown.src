lockdownHelp = function(self)
    text = g.const.ni + g.func.yellow("Usage:","u") + g.const.ni
    text = text + g.func.green(self.name) + g.func.lightgray(" Secures the system by changing permissions on all files and folders to u-rwx, g-rwx, o-rwx. If this is your home system, it leaves the ability to run terminal, sudo, mail, map, chat, settings, and manual. Also deletes all files named Mail.txt, Bank.txt, and passwd.") + g.const.ni
    text = text + g.func.green(self.name) + g.func.cyan(" [-h/help]") + g.func.lightgray(" Displays this help text") + g.const.nl

    print(text)
end function

setPerms = function(obj)
    obj.chmod("u-rwx")
    obj.chmod("g-rwx")
    obj.chmod("o-rwx")
end function

setHomePerms = function(obj)
    obj.chmod("u+rx")
    obj.chmod("g+rx")
end function

doLockdown = function(dir, home, excludes)
    print(g.const.indent + g.func.lightgray("Locking " + dir.path + "..."))

    for folder in dir.get_folders
        doLockdown(folder, home, excludes)

        setPerms(folder)
    end for

    for file in dir.get_files
        if file.name == "Mail.txt" or file.name == "Bank.txt" or file.name == "passwd" then
            print(g.const.indent + g.func.orange("Deleting " + file.path + "..."))
            file.delete
        end if

        if home and typeof(excludes.indexOf(file.name)) != "number" then
            setPerms(file)
        else if home and typeof(excludes.indexOf(file.name)) == "number" then
            setPerms(file)
            setHomePerms(file)
        else
            setPerms(file)
        end if
    end for

    return
end function

lockdown = function(args = null)
    if args != null and args.len > 0 then
        if args[0] == "help" or args[0] == "-h" then
            self.help
            return
        end if
    end if

    excludes = ["sudo","Terminal","Terminal.exe","Mail","Mail.exe","Map","Map.exe","Chat","Chat.exe","Settings.exe","Manual","Manual.exe"]

    comp = get_shell.host_computer

    guest = comp.File("/home/guest")

    if (guest != null) then
        print(g.const.indent + g.func.orange("Deleting guest directory..."))
        guest.delete
    end if

    procs = comp.show_procs.split(char(10))

    home = 0

    for proc in procs
        name = proc.split(" ")[4]

        if name == "Xorg" then home = 1
    end for

    root = comp.File("/")

    if (home) then print(g.const.indent + g.func.cyan("Home computer detected...") + g.const.nl)

    doLockdown(root, home, excludes)

    print(g.const.ni + g.func.green("System locked down."))
    wait(2)
    g.tools.clear.exec
    return
end function

// Fill out the parts of this template, and then place it in the a folder called 'tools' in the same directory as the main hex binary file.
// You can leave it as src code (name it with a .src file extension), otherwise hex will attempt to load it as a binary file that has been built with the
// 'allow import' flag set.

g.tools.lockdown = {
    "name": "lockdown",         // Unformatted tool name text
    "text": g.func.cyan("lockdown"),    // Tool name text with any formatting you want to use when its name is displayed
    "sortOrder": 500,           // This number will be used to sort this tool in the menu.  Quit is the last built-in item, with sort order 999.
    "exec": @lockdown,          // A function reference to the toolName function above.
    "help": @lockdownHelp,      // A function reference to the help function above.
    "data": {}                  // This map can be used to store any data or additional functions used by the tool that might need to be accessed outside of the tool itself.
}