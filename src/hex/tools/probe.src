help = function() 
    text = const.ni + "Usage:".yellow("u") + const.ni
    text = text + "probe".green + " [ip]".cyan + " Displays port and whois information for the specified ip address".lightgray
    text = text + lightgray(" (also sets " + const.targettext + " to specified ip address and port 0).") + const.ni
    text = text + "probe".green + " [-s/show]".cyan + " Displays previously scanned port and whois information".lightgray + const.ni
    text = text + "probe".green + " [-h/help]".cyan + " Displays this help text".lightgray + const.nl

    print(text)
end function

probe = function(args)
    if args.len == 0 or args.len > 1 or args[0] == "-h" or args[0] == "help" then
        tools.probe.help
        return
    end if

    if args[0] == "-s" or args[0] == "show" then
        if tools.probe.data.hasIndex("saved") then
            print(tools.probe.data.saved)
        else
            print(const.indent + error("No saved probe information."))
        end if
        return
    end if

    if is_valid_ip(args[0]) then
        target.ip = args[0]
        target.port = 0
    else
        print(const.indent + error("Invalid argument: " + args[0]))
        return
    end if

    isLan = is_lan_ip(target.ip)

    if isLan then
        router = get_router(target.ip)
        if router then
            isRouter = 1
        else
            isRouter = 0
            router = get_router
        end if
    else
        router = get_router(target.ip)
        isRouter = 1
    end if

    if not router then
        print(const.indent + error("Could not connect to IP: " + target.ip))
        return
    end if

    adminInfo = whois(target.ip)
    infoLines = adminInfo.split(char(10))

    infoObject = {}
    
    infoObject.domain = infoLines[0].split(":")[1].trim
    infoObject.admin = infoLines[1].split(":")[1].trim
    infoObject.email = infoLines[2].split(":")[1].trim
    infoObject.phone = infoLines[3].split(":")[1].trim

    output = const.nl + const.indent + const.llb + const.ni
    output = output + format("[" + router.public_ip.cyan + "] [" + green("kernel_router.so: " + router.kernel_version) + "]", "b") + const.ni
    output = output + format("[" + magenta("admin: " + infoObject.admin) + "] [" + orange("email: " + infoObject.email) + "]", "b") + const.ni
    output = output + format("[" + yellow("domain: " + infoObject.domain) + "] [" + red("phone: " + infoObject.phone) + "]", "b") + const.ni
    output = output + const.llb + const.nl + const.ni
    
    if router.firewall_rules.len > 0 then
        output = output + yellow("<size=18>Firewall Rules</size>") + const.ec + const.ni

        for rule in router.firewall_rules
            output = output + rule + const.ni
        end for
        
        output = output + const.nl + const.ni + const.lb + const.nl + const.ni
    end if

    sPorts = []

    if not isRouter then
        ports = router.device_ports(target.ip)

        sPorts = sPorts + ports

        output = output + target.ip + const.ni

        for port in ports
            output = output + green("- " + port.port_number + const.tab + "[open]" + const.tab + "(" + router.port_info(port) + ")") + const.ni
        end for
    else
        ports = []

        for address in router.devices_lan_ip
            ports = ports + router.device_ports(address)
        end for

        rPorts = []

        sPorts = sPorts + router.used_ports

        for rPort in router.used_ports
            rPorts.push(rPort.port_number + ":" + rPort.get_lan_ip)
        end for

        ports.sort("get_lan_ip")

        devices = {}

        if isRouter then
            if not devices.hasIndex(router.local_ip) then devices[router.local_ip] = []

            devices[router.local_ip].push(green("- 0" + const.tab + const.tab + "[open]" + const.tab + const.tab + "(kernel " + router.kernel_version + ")") + const.ni)
        end if

        for port in ports
            if not devices.hasIndex(port.get_lan_ip) then devices[port.get_lan_ip] = []

            isClosed = 1

            if rPorts.indexOf(port.port_number + ":" + port.get_lan_ip) != null then isClosed = 0

            if port.port_number == 8080 and port.get_lan_ip != router.local_ip then
                devices[port.get_lan_ip].push(red("- 0" + const.tab + const.tab + "[closed]" + const.tab + "(kernel " + router.kernel_version + ")") + const.ni)
            end if

            if not isClosed then
                line = green("- " + port.port_number + const.tab + "[open]" + const.tab + const.tab + "(" + router.port_info(port) + ")") + const.ni
            else
                if port.is_closed then
                    line = red("- " + port.port_number + const.tab + "[closed]" + const.tab + "(" + router.port_info(port) + ")") + const.ni
                else
                    line = orange("- " + port.port_number + const.tab + "[internal]" + const.tab + "(" + router.port_info(port) + ")") + const.ni
                end if
            end if

            if devices[port.get_lan_ip].indexOf(line) == null then devices[port.get_lan_ip].push(line)
        end for

        for index in devices.indexes
            output = output + index.white + const.ni

            lines = devices[index]

            lines.sort

            for line in lines
                output = output + line
            end for
        end for
    end if

    output = output + const.nl

    tools.probe.data.saved = output
    tools.probe.data.ports = sPorts

    output = output + const.ni + "Enter a port to scan or a lan ip address:" + const.ni

    print(output)

    choices = const.indent + yellow("a" + const.tab + const.tab + "(all open ports)") + const.ni

    choices = choices + cyan("0" + const.tab + const.tab +  "(kernel " + router.kernel_version + ")") + const.ni


    for port in sPorts
        if port.port_number > 999 then
            choices = choices + cyan(port.port_number + const.tab + "(" + router.port_info(port) + ")") + const.ni
        else
            choices = choices + cyan(port.port_number + const.tab + const.tab + "(" + router.port_info(port) + ")") + const.ni
        end if
    end for

    choices = choices + red("q" + const.tab + const.tab + "(quit)") + const.nl + const.ni + char(187) + " "

    choice = user_input(choices)

    if (choice == "q") then return

    if is_valid_ip(choice) then
        tools.scan.exec(target.ip + " 0 " + choice)
    else
        tools.scan.exec(target.ip + " " + choice)
    end if
end function

tools.probe = {
    "name": "probe",
    "text": cyan("probe"),
    "sortOrder": 0,
    "exec": @probe,
    "help": @help,
    "data": {
        "ports": [],
        "saved": ""
    }
}